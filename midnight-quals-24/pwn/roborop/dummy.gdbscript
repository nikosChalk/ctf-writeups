
# gdb -x dummy.gdbscript --nh --args ./dummy 1234

set confirm off
set pagination off
set disassembly-flavor intel

b *main
command
    silent

    # Break when mmap returns.
    # To achieve that, we tbreak at mmap entry and then insert a breakpoint
    # at the return address.
    tbreak *mmap
    command
        silent
        tbreak *(*(char**)($rsp))
        command
            silent
            set $addr=$rax
            continue
        end
        continue
    end
    continue
end

b *dlclose
command
    silent
    x/10bx $addr
    dump memory code $addr $addr+0x10000000
    continue
end

set $_exitcode = -999
define hook-stop
    if $_exitcode != -999
        printf "exit code: %d\n", $_exitcode
        quit
    end
end

run

